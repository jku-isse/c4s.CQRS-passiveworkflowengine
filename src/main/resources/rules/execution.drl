package rules;

import org.slf4j.Logger
import org.axonframework.commandhandling.gateway.CommandGateway

import impactassessment.model.workflowmodel.DecisionNodeDefinition.States
import impactassessment.model.workflowmodel.WorkflowInstance
import impactassessment.model.workflowmodel.DecisionNodeInstance
import impactassessment.model.workflowmodel.DecisionNodeDefinition
import impactassessment.model.workflowmodel.TaskDefinition
import impactassessment.model.workflowmodel.WorkflowTask
import impactassessment.model.workflowmodel.TaskLifecycle.Events
import impactassessment.model.workflowmodel.AbstractWorkflowInstanceObject
import impactassessment.model.workflowmodel.WorkflowDefinition
import impactassessment.model.definition.WPManagementWorkflow

import impactassessment.api.CompleteDataflowCmd
import impactassessment.api.ActivateInBranchCmd
import impactassessment.api.ActivateOutBranchCmd

import impactassessment.mock.artifact.Artifact

import java.util.List
import java.util.Set


global CommandGateway commandGateway;
global Logger log;


rule "SimpleTest1"
    when
        Artifact(  )
    then
        log.debug("execution.drl: rule SimpleTest1");
end


rule "AddWPTicketAsInputToNewTasks"
    when // matches any inBranch for the Workflow waiting for input,
    	$a : Artifact(  )
        $dni : DecisionNodeInstance( getWorkflow().getId().equals($a.getField("id")) &&
        							 getWorkflow().getWorkflowDefinition().getId().equals(WPManagementWorkflow.WORKPACKAGE_CHANGE_MANAGEMENT_WORKFLOW_TYPE) &&
									 state == DecisionNodeDefinition.States.PASSED_OUTBRANCH_CONDITIONS
									 , $wfi : getWorkflow() )
									 	@watch ( state )
    then      // creates the task and adds the ticket as input
        log.debug("sending CompleteDataflowCmd from KB "+$wfi.getId());
        commandGateway.send(new CompleteDataflowCmd($wfi.getId(), $dni));

end

rule "Open2InProgress-InBranchConditions"
    when
    	$a : Artifact( getField("status").equals("In Progress") )
        $task : WorkflowTask( getTaskType().getId().equals("open") )
        						@watch ( outputState, lifecycleState )
        $dn : DecisionNodeInstance( getWorkflow().getId().equals($a.getField("id")) &&
        							getWorkflow().getId().equals($task.getWorkflow().getId()) &&
        							getDefinition().getId().equals("open2inProgressOrResolved") &&
        							getInBranchForWorkflowTask($task) != null &&
        							!isTaskCompletionConditionsFullfilled()
        							, $wfi : getWorkflow() )
    then
        log.debug("Transition from OPEN to IN_PROGRESS for triggertask: "+$task.toString());
        log.debug("sending ActivateInBranchCmd from KB");
        commandGateway.send(new ActivateInBranchCmd($wfi.getId(), $dn, $task));
        log.debug("sending ActivateOutBranchCmd from KB");
        commandGateway.send(new ActivateOutBranchCmd($wfi.getId(), $dn, "inProgressIn"));
end

rule "InProgress2Resolved-InBranchConditions"
    when
    	$a : Artifact( getField("status").equals("Resolved") )
        $task : WorkflowTask( getTaskType().getId().equals("inprogress"))
        						@watch ( outputState, lifecycleState )
        $dn : DecisionNodeInstance( getWorkflow().getId().equals($a.getField("id")) &&
        							getWorkflow().getId().equals($task.getWorkflow().getId()) &&
        							getDefinition().getId().equals("inProgressToResolved") &&
        							getInBranchForWorkflowTask($task) != null &&
        							!isTaskCompletionConditionsFullfilled()
        							, $wfi : getWorkflow() )
    then
        log.debug("Transition from IN_PROGRESS to RESOLVED for triggertask: "+$task.toString());
		log.debug("sending ActivateInBranchCmd from KB");
        commandGateway.send(new ActivateInBranchCmd($wfi.getId(), $dn, $task));
        log.debug("sending ActivateOutBranchCmd from KB");
        commandGateway.send(new ActivateOutBranchCmd($wfi.getId(), $dn, "resolvedIn"));
end

rule "Open2Resolved-InBranchConditions"
    when
    	$a : Artifact( getField("status").equals("Resolved") || getField("status").equals("Closed") )
        $task : WorkflowTask( getTaskType().getId().equals("open") )
        						@watch ( outputState, lifecycleState )
        $dn : DecisionNodeInstance( getWorkflow().getId().equals($a.getField("id")) &&
        							getWorkflow().getId().equals($task.getWorkflow().getId()) &&
        							getDefinition().getId().equals("open2inProgressOrResolved") &&
        							getInBranchForWorkflowTask($task) != null &&
        							!isTaskCompletionConditionsFullfilled()
        							, $wfi : getWorkflow() )
    then
        log.info("Transition from OPEN to RESOLVED for triggertask: "+$task.toString());
		log.debug("sending ActivateInBranchCmd from KB");
        commandGateway.send(new ActivateInBranchCmd($wfi.getId(), $dn, $task));
        log.debug("sending ActivateOutBranchCmds from KB");
        commandGateway.send(new ActivateOutBranchCmd($wfi.getId(), $dn, "inProgressIn"));
        commandGateway.send(new ActivateOutBranchCmd($wfi.getId(), $dn, "resolvedIn"));
end